prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
prefix sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
prefix sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dbpedia: <http://dbpedia.org/resource/>
prefix dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT  ?personId
        ?personFirstName
        ?personLastName
        ?personGender
        ?personCityName
        (COUNT(*) AS ?commonInterestScore)
WHERE {
?gPer           snvoc:id                        ?id;
                snvoc:knows/snvoc:hasPerson     ?fPer;
                snvoc:hasInterest               ?tag .
?fPer           snvoc:knows/snvoc:hasPerson     ?sPer .
?sPer           snvoc:id                        ?personId;
                snvoc:firstName                 ?personFirstName;
                snvoc:lastName                  ?personLastName;
                snvoc:gender                    ?personGender;
                snvoc:birthday                  ?bd;
                snvoc:isLocatedIn/foaf:name     ?personCityName .
?post           snvoc:hasCreator                ?sPer;
                snvoc:hasTag                    ?tag .
FILTER(?gPer != ?sPer) .
BIND((?hs0 + 1) AS ?hs1) .
FILTER((MONTH(?bd) = ?hs0 && DAY(?bd) >= 21) || (MONTH(?bd) = ?hs1 && DAY(?bd) <= 22)) .
}
GROUP BY ?personId ?personFirstName ?personLastName ?personGender ?personCityName
ORDER BY DESC(?commonInterestScore) ?personId
LIMIT 10