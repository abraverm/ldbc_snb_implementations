prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
prefix sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
prefix sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dbpedia: <http://dbpedia.org/resource/>
prefix dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT  ?friendId
        ?friendLastName
        (MIN(?dist) AS ?distanceFromPerson)
        ?friendBirthDate
        ?friendCreationDate
        ?friendGender
        ?friendBrowserUsed
        ?freindLocationIp
        (GROUP_CONCAT(DISTINCT ?email;separator=', ') AS ?friendEmails)
        (GROUP_CONCAT(DISTINCT ?lang;separator=', ') AS ?friendLanguages)
        ?friendCityName
        (GROUP_CONCAT(DISTINCT ?univInfo;separator=', ') AS ?friendUniversities)
        (GROUP_CONCAT(DISTINCT ?compInfo;separator=', ') AS ?friendCompanies)
WHERE {
{
        {
                SELECT DISTINCT ?frPer (1 AS ?dist) WHERE { ?gPer snvoc:knows/snvoc:hasPerson ?frPer  }
        }
        UNION
        {
                SELECT DISTINCT ?frPer (2 AS ?dist) WHERE { ?gPer snvoc:knows/snvoc:hasPerson/snvoc:knows/snvoc:hasPerson ?frPer . FILTER(?gPer != ?frPer)  }
        }
        UNION
        {
                SELECT DISTINCT ?frPer (3 AS ?dist) WHERE { ?gPer snvoc:knows/snvoc:hasPerson/snvoc:knows/snvoc:hasPerson ?oPer . ?oPer snvoc:knows/snvoc:hasPerson ?frPer FILTER(?gPer != ?frPer && ?gPer != ?oPer)  }
        }
}
?gPer           snvoc:id                        ?id .
?frPer          snvoc:id                        ?friendId;
                snvoc:firstName                 ?fn;
                snvoc:lastName                  ?friendLastName;
                snvoc:birthday                  ?friendBirthDate;
                snvoc:creationDate              ?friendCreationDate;
                snvoc:gender                    ?friendGender;
                snvoc:browserUsed               ?friendBrowserUsed;
                snvoc:locationIP                ?freindLocationIp;
                snvoc:email                     ?email;
                snvoc:speaks                    ?lang;
                snvoc:studyAt                   ?stud;
                snvoc:isLocatedIn/foaf:name     ?friendCityName;
                snvoc:workAt                    ?work .
?stud           snvoc:classYear                 ?clYear;
                snvoc:hasOrganisation           ?univ .
?univ           snvoc:isLocatedIn/foaf:name     ?uCityName;
                foaf:name                       ?uName .
?work           snvoc:workFrom                  ?wFrom;
                snvoc:hasOrganisation           ?comp .
?comp           snvoc:isLocatedIn/foaf:name     ?counName;
                foaf:name                       ?cName .
BIND(CONCAT(?uName, ", ", STR(?clYear), ", ", ?uCityName ) AS ?univInfo) .
BIND(CONCAT(?cName, ", ", STR(?wFrom), ", ", ?counName ) AS ?compInfo) .
}
GROUP BY ?friendId ?friendLastName ?friendBirthDate ?friendCreationDate ?friendGender ?friendBrowserUsed ?freindLocationIp ?friendCityName
ORDER BY ?distanceFromPerson ?friendLastName ?friendId
LIMIT 20
